//Music Downloader 初始化设置
//Copyright (C) 2015 一个二百五2 All Rights Reserved.版权所有

Scripts.execStorage("LanguageProcess.tjs");
//outPutBaseLanguageFile();
loadLanguageFile("baselanguage.language");

//二重启动检查
// g行可能ファイルのパスをキ`にしてロックを行う
if( !System.createAppLock("f739ab17-7787-4efc-81b2-5f0073c6614a") )
{
	// すでに起婴筏皮い
	System.inform(language.description.MultipleStartupCheckDescription);
	System.exit();
}

function checkVersion( versiona, versionb){
	var versiona_splited, versionb_splited = [];
	versiona_splited = versiona.split(".");
	versionb_splited = versionb.split(".");
	for(var i = 0; i < versiona_splited.count; i++){
		if( int( versiona_splited[i] ) < int( versionb_splited[i] ) ){
			return false;
		}
	}
	return true;
}

var QMS_UserUUID;
try{
	var Temp = [];
	Temp.load( System.exePath + "savedata/Userdata.dat");
	QMS_UserUUID = Temp[5];
}catch{
}
if (QMS_UserUUID == void || QMS_UserUUID.trim() == ""){
	QMS_UserUUID = System.createUUID();
}

System.title = "Music Downloader";

Debug.clearLogFileOnError = true;
//异常处理
System.exceptionHandler = function(e)
{
	Debug.startLogToFile(true);
	System.inform(language.error.RuntimeError);
	BugReporter_HR.setRequestHeader("Content-Type", "application/x-www-form-urlencoded"); 
	var user_qq = System.inputString(language.ui.ErrorReportTitle,language.description.ErrorReportDescription,"");
	var Temp = %[];
	try{
		Temp.QMS_MainVersion = QMS_MainVersion;
		Temp.QQMSVersion = QQMSVersion;
		Temp.NEMSVersion = NEMSVersion;
	}catch(e){
		Temp.QMS_MainVersion = Temp.QQMSVersion = Temp.NEMSVersion = "无法获取 原因:"+e.message;
	}
	var rept = "User_QQ="+user_qq+"&Exception="+e.message+"&Version="+Temp.QMS_MainVersion+"&QQMusicServiceVersion=" + Temp.QQMSVersion  + "&NetEastMusicServiceVersion="+Temp.NEMSVersion + "&UserUUID=" + QMS_UserUUID;
	BugReporter_HR.open( "POST", "http://exception.ac130.top:8880/Exceptions/MusicDownloader");
	BugReporter_HR.send( rept);
	waitforSendExpention_timer.enabled = 1;
	System.inform(language.description.ErrorReportReportingDescription);
	return true;
};

var waitforSendExpention_timer = new Timer( waitforSendExpention,"");
waitforSendExpention_timer.enabled = 0;
waitforSendExpention_timer.interval = 10;
function waitforSendExpention(){
	if(BugReporter_HR.readyState == global.HttpRequest.LOADED){
		System.terminate();
	}
}

var userdata = [];
var userdataloaded = false;
try{
	userdata.load( System.exePath + "savedata/Userdata.dat");
	userdataloaded = true;
}catch(e){
	userdataloaded = false;
}

//添加自动路径
var addAP = Storages.addAutoPath;

addAP("baseSupport/");
addAP("dll/");
addAP("scripts/");

delete addAP;

//加载插件
function addPlugin(name){
	try{
		Plugins.link(name);
	}catch(e){
		Debug.message(e.message);
	}
}
addPlugin("fstat.dll");
addPlugin("httprequest.dll");
addPlugin("json.dll");
addPlugin("systemEx.dll");
addPlugin("KSoundPlayer.dll");
addPlugin("saveStruct.dll");
delete addPlugin;

//错误自动上报
var BugReporter_Window = new Window();
BugReporter_Window.visible = false;

var BugReporter_HR = new HttpRequest(BugReporter_Window, true);

//加载运行库
function loadScripts(name){
	try{
		Scripts.execStorage(name);
	}catch(e){
		Debug.message(e.message);
	}
}
loadScripts("KAGLayer.tjs");
loadScripts("EditLayer.tjs");
//加载服务
loadScripts("NetEastMusicService.tjs");
loadScripts("QQMusicService.tjs");
//加载主窗口
loadScripts("searchwindow.tjs");
//加载更新组件
//注入更新组件
loadScripts("Updater.tjs");
//注入播放组件
loadScripts("Player.tjs");
delete loadScripts;

try{
	if( userdataloaded ){
		QMS_songsavepath = userdata[0];
		QMS_mvsavepath = userdata[1];
		QMS_SongKb = userdata[2];
		QMS_MVKb = userdata[3];
		QMS_MainBuffer.looping = userdata[4];
		QMS_MenuSetting_lrcsave_utf8.checked = userdata[6];
	}
}catch(e){
	Debug.message(e.message);
	System.inform(language.error.ReadUserSettingsError,language.ui.InitializationError1);
}

if (QMS_songsavepath == ''){
	QMS_songsavepath = Storages.getFullPath(System.exePath + "Download/");
}
if (QMS_mvsavepath == ''){
	QMS_mvsavepath = Storages.getFullPath(System.exePath + "Download/");
}

if ( QMS_SongKb == "128"){
	QMS_SongBrList_128kb.checked = 1;
}
if ( QMS_SongKb == "320"){
	QMS_SongBrList_320kb.checked = 1;
}
if ( QMS_SongKb == "flac"){
	QMS_SongBrList_flac.checked = 1;
}
if ( QMS_MVKb == "360" ){
	QMS_MVBrList_360p.checked = 1;
}
if ( QMS_MVKb == "480" ){
	QMS_MVBrList_480p.checked = 1;
}
if ( QMS_MVKb == "720" ){
	QMS_MVBrList_720p.checked = 1;
}
if ( QMS_MVKb == "1080" ){
	QMS_MVBrList_1080p.checked = 1;
}
if ( userdata[4] == 1 ){
	QMS_MainBuffer.looping = 1;
}else{
	QMS_MainBuffer.looping = 0;
}
QMS_PlayerControl_loop.checked = QMS_MainBuffer.looping;
if ( userdata[6] == ''){
	QMS_MenuSetting_lrcsave_utf8.checked = true;
}

QMS_searchMainWindow.onCloseQuery = function{
	userdata[0] = Storages.getLocalName( QMS_songsavepath );
	userdata[1] = Storages.getLocalName( QMS_mvsavepath );
	userdata[2] = QMS_SongKb;
	userdata[3] = QMS_MVKb;
	userdata[4] = QMS_MainBuffer.looping;
	userdata[5] = QMS_UserUUID;
	userdata[6] = QMS_MenuSetting_lrcsave_utf8.checked;
	userdata[userdata.count] = "#%s".sprintf(language.description.WarningInUserSettingsFile);
	userdata.save( System.exePath + "savedata/Userdata.dat","z");
	System.exit();
};

//反馈
var QMS_MenuSetting_report = new MenuItem( QMS_searchMainWindow,language.menu.Feedback);
QMS_MenuSetting.add( QMS_MenuSetting_report);

var SuggestionReporter_Window = new Window();
SuggestionReporter_Window.visible = false;

var SuggestionReporter_HR = new HttpRequest(SuggestionReporter_Window, true);

QMS_MenuSetting_report.onClick = function(){
	BugReporter_HR.setRequestHeader("Content-Type", "application/x-www-form-urlencoded"); 
	var user_suggestion = System.inputString(language.ui.FeedbackTitle,language.description.FeedbackPleaseInputYourFeedback,"");
	if ( user_suggestion == void || user_suggestion.trim() == ""){
		return 0;
	}
	var user_qq = System.inputString(language.ui.FeedbackTitle,language.description.FeedbackPleaseInputYourContactDetails,"");
	var Temp = %[];
	try{
		Temp.QMS_MainVersion = QMS_MainVersion;
	}catch(e){
		Temp.QMS_MainVersion = Temp.QQMSVersion = Temp.NEMSVersion = "无法获取 原因:"+e.message;
	}
	var rept = "User_QQ="+user_qq+"&Suggestion="+user_suggestion+"&Version="+Temp.QMS_MainVersion+"&UserUUID="+QMS_UserUUID;
	BugReporter_HR.open( "POST", "http://exception.ac130.top:8880/Suggestions/MusicDownloader");
	BugReporter_HR.send( rept);
	System.inform(language.description.FeedbackThankyou,language.ui.FeedbackTitle);
};

//设置界面注入关于
var QMS_MenuSetting_about = new MenuItem(QMS_searchMainWindow,language.menu.About);
QMS_MenuSetting.add(QMS_MenuSetting_about);
QMS_MenuSetting_about.onClick = function{
	System.inform("Music Downloader v"+QMS_MainVersion+
					"\n\nCopyright (C) 2015 一个二百五2 All Rights Reserved.版权所有\n本程序遵循GPLv3协议\n\nKSoundPlayer Plugin API by khjxiaogu\n\n用户通用唯一识别码: "+QMS_UserUUID,"关于");
};

//运行保护
///*
delete Plugins;
delete Scripts.eval;
delete Scripts.exec;
System.setArgument("-debugwin","no");
System.setArgument("-hkcontroller","");
System.setArgument("-hkeditor","");
System.setArgument("-hkwatch","");
System.setArgument("-hkconsole","");
System.setArgument("-hkupdaterect","");
System.setArgument("-debug","no");
System.setArgument("-logerror","clear");
//*/
Debug.console.visible = false;